plugins {
    id 'com.android.application'
}

// build.gradle in the 'app' module

android {
    compileSdk = rootProject.ext.CompileSdk

    defaultConfig {
        applicationId = "com.donnnno.arcticons"
        minSdk = rootProject.ext.MinSdk
        targetSdk = rootProject.ext.TargetSdk
        versionCode = 1520
        versionName = '14.3.3'
        multiDexEnabled = true
    }

    signingConfigs {
        release
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs.named("release") {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile rootProject.file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    } else {
        signingConfigs.named("release") {
            initWith(signingConfigs.debug)
        }
    }


    buildTypes {
        configureEach {
            signingConfig = signingConfigs.getByName("release")
        }
        release {
            minifyEnabled = false
        }
        debug {
            applicationIdSuffix = '.debug'
        }
    }

    flavorDimensions += "variant"
    productFlavors {
        normal {
            dimension = "variant"
            resValue  "string", "app_name", "Arcticons"
        }
        normalPlay {
            dimension = "variant"
            resValue "string", "app_name", "Arcticons"
        }
        black {
            dimension = "variant"
            //noinspection GradlePath
            applicationIdSuffix = '.light'
            resValue "string", "app_name", "Arcticons Black"
        }
        blackPlay {
            dimension = "variant"
            applicationIdSuffix = '.light'
            resValue "string", "app_name", "Arcticons Black"
        }
        you {
            dimension = "variant"
            minSdk = 26
            applicationIdSuffix = '.you'
            resValue "string", "app_name", "Arcticons Material You"
        }
        youPlay {
            dimension = "variant"
            minSdk = 26
            applicationIdSuffix = '.you.play'
            resValue "string", "app_name", "Arcticons Material You"
        }
        dayNight {
            dimension = "variant"
            minSdk = 29
            applicationIdSuffix = '.daynight'
            resValue "string", "app_name", "Arcticons Day & Night"
        }
    }

    sourceSets {
        normal { res.srcDirs = ['src/normalFree/res/', 'src/normal/res'] }
        normalPlay { res.srcDirs = ['src/normalPlay/res/', 'src/normal/res'] }
        black { res.srcDirs = ['src/blackFree/res/', 'src/black/res'] }
        blackPlay { res.srcDirs = ['src/blackPlay/res/', 'src/black/res'] }
		you { res.srcDirs = ['src/youFree/res/', 'src/you/res'] }
        youPlay { res.srcDirs = ['src/youPlay/res/', 'src/you/res']
                manifest.srcFile 'src/you/AndroidManifest.xml'}
        dayNight { res.srcDirs = ['src/dayNightFree/res/', 'src/dayNight/res'] }
    }

    base {
        // Output file name
        archivesName = "${parent.name}-${android.defaultConfig.versionName}"
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    buildFeatures {
        resValues = true
        buildConfig = true
        viewBinding = true
    }
    namespace = 'com.donnnno.arcticons'
    lint {
        abortOnError = false
        checkReleaseBuilds = false
        lintConfig = file("lint.xml")
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

androidComponents {
    onVariants(selector().all()) { variant ->
        project.afterEvaluate {

            def flavor = variant.flavorName
            def helperTaskName = null

            if (flavor.contains("you")) {
                helperTaskName = "runyou"
            } else if (flavor.contains("black")) {
                helperTaskName = "runblack"
            } else if (flavor.contains("normal")) {
                helperTaskName = "runnormal"
            } else if (flavor.contains("dayNight")) {
                helperTaskName = "rundayNight"
            }

            if (helperTaskName != null) {
                tasks.named("pre${variant.name.capitalize()}Build") {
                    dependsOn(":preparehelper:${helperTaskName}")
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    normalPlayImplementation 'com.github.zixpo:candybar:main-SNAPSHOT'
    blackPlayImplementation 'com.github.zixpo:candybar:main-SNAPSHOT'
    youPlayImplementation 'com.github.zixpo:candybar:main-SNAPSHOT'
    normalImplementation 'com.github.Donnnno:candybar-foss:3.22.2'
    blackImplementation 'com.github.Donnnno:candybar-foss:3.22.2'
    youImplementation 'com.github.Donnnno:candybar-foss:3.22.2'
    dayNightImplementation 'com.github.Donnnno:candybar-foss:3.22.2'
}

import com.android.tools.profgen.ArtProfileKt
import com.android.tools.profgen.ArtProfileSerializer
import com.android.tools.profgen.DexFile

// F-Droid Reproducible Build tasks

tasks.configureEach { task ->
    if (task.name.startsWith("compile") && task.name.endsWith("ReleaseArtProfile")) {
        task.doLast {
            outputs.files.each { file ->
                if (file.name.endsWith(".profm")) {
                    println("Sorting ${file} ...")
                    def version = ArtProfileSerializer.valueOf("METADATA_0_0_2")
                    def profile = ArtProfileKt.ArtProfile(file)
                    def keys = new ArrayList(profile.profileData.keySet())
                    def sortedData = new LinkedHashMap()
                    Collections.sort(keys, new DexFile.Companion())
                    keys.each { key -> sortedData[key] = profile.profileData[key] }
                    new FileOutputStream(file).with {
                        write(version.magicBytes$profgen)
                        write(version.versionBytes$profgen)
                        version.write$profgen(it, sortedData, "")
                    }
                }
            }
        }
    }
}
