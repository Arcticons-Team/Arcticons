// TODO: Remove `/*` and `*/` below to enable OneSignal
/*
buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:0.13.4'
    }
}
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
*/
apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword 'candybar'
            storeFile file('candybar.jks')
            storePassword 'candybar'

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdkVersion rootProject.ext.CompileSdk

    defaultConfig {
        applicationId 'com.candybar.dev'
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode rootProject.ext.VersionCode * 10 + 0
        versionName rootProject.ext.VersionName
        multiDexEnabled true

        // This code loads the license key from `local.properties` file
        // and saves it as build config field named "LICENSE_KEY"
        String licenseKey = ''
        Properties props = new Properties()
        File propFile = rootProject.file('local.properties')
        if (propFile.exists()) {
            props.load(propFile.newDataInputStream())
            licenseKey = props.get('license_key', '')
        }
        buildConfigField 'String', 'LICENSE_KEY', "\"$licenseKey\""
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':library')

    // TODO: Remove `//` below to enable OneSignal
    //implementation 'com.onesignal:OneSignal:[4.0.0, 4.99.99]'
}